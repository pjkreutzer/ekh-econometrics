*******************************************
*             EKHM66 Fall 2020            *
*          Lab 2: OLS regression          *
*******************************************

/*TIP: EXECUTE COMMANDS FROM THE DO-FILE BY HIGHLIGHTING THE COMMANDS AND PRESSING CTRL+D.
PRESS THE PAGE UP KEY TO RETRIEVE THE MOST RECENTLY EXECUTED COMMAND TO THE COMMAND WINDOW.*/

* LAST TIME, WE EXAMINED CAUSALITY AND THE EXPERIMENTAL IDEAL.
* WE ANALYSED NON-EXPERIMENTAL AND EXPERIMENTAL DATA ON HEALTH INSURANCE AND HEALTH STATUS.
* WE CONCLUDED THAT THE TWO TYPES OF DATA CAN PRODUCE ENTIRELY DIFFERENT CONCLUSIONS DUE TO SELECTION ISSUES.
* WHILE USING EXPERIMENTAL DATA SOLVES THE PROBLEM, IT IS HARD TO COME BY.
* THUS, WE NORMALLY HAVE TO MAKE DO WITH NON-EXPERIMENTAL DATA.
* NOW, WE EXAMINE ORDINARY LEAST SQUARES REGRESSION AND MULTIVARIATE MODELS - THE CONTROL STRATEGY (OLS WITH CONTROL VARIABLES, AIMING TO FULFIL THE CIA).
* CONDITIONAL INDEPENDENCE ASSUMPTION = CONTROL AND TREATMENT GROUPS ARE/HAVE BEEN MADE ON AVERAGE EQUAL IN TERMS OF ALL RELEVANT FACTORS.

***SETTING THE WORKING DIRECTORY***

cd "\\uwfpcluster01.uw.lu.se\el8285la$\Documents\PhD programme\Teaching\EKHM66 Econometrics II, Ht 2020\Labs\Lab 2 (OLS regression)"


***STARTING A LOG FILE***

log using "lab 2.log", replace

***OLS REGRESSION***

use NHIS2009_clean_subsample2.dta, clear
* WE USE THE NON-EXPERIMENTAL DATA FROM THE 2009 NATIONAL HEALTH INTERVIEW SURVEY (NHIS).
* OUR RESEARCH QUESTION IS "WHAT IS THE CAUSAL EFFECT OF HEALTH INSURANCE ON HEALTH OUTCOMES?"

* LAST TIME, WE SAW THAT THERE IS SELECTION INTO TREATMENT (=HAVING INSURANCE) BASED ON OBSERVABLES.
* CAN WE THEN REASONABLY ESTIMATE THE EFFECTS OF HAVING HEALTH INSURANCE ON HEALTH STATUS USING NON-EXPERIMENTAL DATA AND OLS REGRESSION?
* WE NEED TO (AT LEAST MOSTLY) ELIMINATE BIAS FROM SELECTION INTO TREATMENT.
* =CETERIS PARIBUS CONDITION FULFILLED & CONDITIONAL INDEPENDENCE ASSUMPTION (CIA) HOLDS.


***SELECTION BIAS***

* MEN
tabstat nwhite age yedu famsize empl inc [aw=perweight] if fml==0, by(hi) stat(mean) format(%4.2f)
* WOMEN
tabstat nwhite age yedu famsize empl inc [aw=perweight] if fml==1, by(hi) stat(mean) format(%4.2f)

foreach gender in 0 1 {
	foreach var in nwhite age yedu famsize empl inc {
		reg `var' i.hi if fml==`gender' [aw=perweight], robust
		display "`var' where female = `gender'"
	}
}
* IN LAB 1, WE CHECKED FOR BALANCES (=DIFFERENCES IN DEMOGRAPHIC BACKGROUND VARIABLES) ACROSS THE UNTREATED/TREATED.
* THE PURPOSE WAS TO LOOK FOR EVIDENCE OF SELECTION INTO TREATMENT.
* WE FOUND THAT THERE WERE DIFFERENCES IN NEARLY ALL OF THE DEMOGRAPHIC VARIABLES ACROSS UNTREATED/TREATED GROUPS.
* THIS MEANS THE CETERIS PARIBUS CONDITION IS NOT FULFILLED AND OUR ESTIMATES WILL BE BIASED.
 

* WHAT WOULD HAVE BEEN THE PERFECT EXPERIMENT TO ANSWER OUR RESEARCH QUESTION?
* HOW CAN WE COME AS CLOSE AS POSSIBLE TO THAT USING NON-EXPERIMENTAL DATA?


***BIVARIATE OLS REGRESSION***

reg hlth i.hi [aw=perweight], robust
* ROBUST STANDARD ERRORS DEAL WITH HETEROSKEDASTICITY OF ERROR TERMS.
* WEIGHT USED BECAUSE THE MAKERS OF THE DATA RECOMMEND IT.

* DO WE THINK THIS MODEL AND THE ESTIMATED COEFFICIENT OF HEALTH INSURANCE COVERAGE ARE SUFFERING FROM OMITTED VARIABLE BIAS?
* THEN, HOW DOES OVB IMPACT OUR ESTIMATE?


****OMITTED VARIABLE BIAS***

* HERE, WE SIMULATE DATA TO SHOW HOW OMITTED VARIABLE BIAS WORKS AND WHAT IT CAN DO TO YOUR RESULTS. 
* PARTS OF THE CODE ARE TAKEN FROM: https://blog.stata.com/2016/05/23/understanding-omitted-confounders-endogeneity-omitted-variable-bias-and-related-concepts/
* IN THE COURSE BOOK, OVB IS DISCUSSED IN CHAPTER 2, P. 89 AND ONWARDS. 

* WHEN REGRESSORS ARE UNCORRELATED:

// Generating non-correlated regressors 
clear
capture set seed 111 // ENSURE THE SAMPLE CAN BE REPRODUCED
quietly set obs 20000 // SET NUMBER OF OBSERVATIONS

generate x1 = rnormal() // GENERATE X1 - A NORMALLY DISTRIBUTED VARIABLE
hist x1
generate x2 = 0.5 + rnormal() // GENERATE X2 - A NORMALLY DISTRIBUTED VARIABLE
hist x2
generate y = 1 + x1 - x2 + rnormal() // GENERATE Y - A COMBINATION OF X1 AND X2
hist y

* A GOOD WAY TO EXAMINE CORRELATIONS BETWEEN VARIABLES IS WITH 'correlate' and 'pwcorrelate'.
* THESE PRODUCE PEARSON'S CORRELATION COEFFICIENT AND A TEST OF SIGNIFICANCE IF SPECIFIED.
pwcorr y x1, star(.05)
pwcorr y x2, star(.05)
pwcorr x1 x2, star(.05)

// Generating Model
 
scatter y x1 
scatter y x2 // Y CORRELATES WITH BOTH X1 AND X2
scatter x1 x2 // X1 AND X2 DO NOT SEEM TO BE CORRELATED

regress x2 x1 // NULL HYPOTHESIS IS THAT X1 IS INDEPENDENT OF X2

regress y x1 // SHORT
eststo m1
regress y x1 x2 // LONG
eststo m2
esttab m1 m2

* TEST WHETHER THE BETA COEFFICIENTS OF X1 IN THE TWO MODELS ARE STATISTICALLY THE SAME (HAUSMAN TEST).
hausman m1 m2
* WE CANNOT REJECT THE NULL HYPOTHESIS THAT THERE IS NO SYSTEMATIC DIFFERENCE IN THE ESTIMATED COEFFICIENTS OF X1.

* WHEN THE REGRESSORS ARE UNCORRELATED, THE COEFFICIENT FOR X1 DOES NOT CHANGE MARKEDLY WHEN X2 IS INCLUDED IN THE MODEL.
* THIS SHOWS THAT IF AN OMITTED VARIABLE DOES NOT CORRELATE WITH YOUR INDEPENDENT VARIABLE OF INTEREST, OMITTING IT WILL NOT BIAS THE RESULTS.

* THE BOOK STATES: "SHORT EQUALS LONG PLUS THE EFFECT OF OMITTED TIMES THE REGRESSION OF OMITTED ON INCLUDED."
* THE REGRESSION 'regress x2 x1' HAS A COEFFICIENT VERY CLOSE TO ZERO, WHICH MEANS THAT THE SHORT EQUATION IS JUST AS GOOD AS THE LONG 
* FOR ESTIMATING THE EFFECT OF X1 ON Y. 
* THE COEFFICIENT OF X2 IN THE LONG EQUATION CAN BE MULTIPLIED WITH THE ZERO, WHICH MEANS THAT OMITTED VARIABLE BIAS FROM X2 DOES NOT EXIST.

* WHEN REGRESSORS ARE CORRELATED:

// Generating correlated regressors 
clear
capture set seed 111
quietly set obs 20000
 
generate x1 = rnormal() // GENERATE X1 - A NORMALLY DISTRIBUTED VARIABLE
generate x2 = -0.5 * x1 + rnormal() // GENERATE X2 - WHICH IS NEGATIVE AND HALF OF X1, PLUS A NORMALLY DISTRIBUTED ERROR TERM
generate y   = 1 + x1 - x2 + rnormal() // GENERATE Y - A COMBINATION OF X1 AND X2

scatter y x1 
scatter y x2 // Y CORRELATES WITH BOTH X1 AND X2
scatter x1 x2 // THERE IS A CORRELATION BETWEEN X1 AND X2

* PEARSON'S CORRELATION COEFFICIENT
pwcorr y x1, star(.05)
pwcorr y x2, star(.05)
pwcorr x1 x2, star(.05)

// Generating Model
 
regress y x1 // SHORT
eststo m1
regress y x1 x2 // LONG
eststo m2
esttab m1 m2

* TEST WHETHER THE BETA COEFFICIENTS OF X1 IN THE TWO MODELS ARE STATISTICALLY THE SAME (HAUSMAN TEST).
hausman m1 m2
* WE REJECT THE NULL HYPOTHESIS THAT THERE IS NO SYSTEMATIC DIFFERENCE IN THE ESTIMATED COEFFICIENTS OF X1.

* THE COEFFICIENT FOR X1 CHANGES WHEN X2 IS INCLUDED IN THE REGRESSION MODEL.
* THIS SHOWS THAT WHEN AN OMITTED VARIABLE CORRELATES WITH BOTH YOUR REGRESSOR OF INTEREST 
* AND THE OUTCOME, YOU SHOULD INCLUDE IT IN THE MODEL.


* ILLUSTATING WHAT OMITTING X2 DOES TO OUR ESTIMATE OF THE EFFECT OF X1 ON Y:

regress x2 x1
predict res_x2_x1, resi //THE PART OF X1 THAT CANNOT BE PREDICTED BY X2

scatter y res_x2_x1
* THE RESIDUALS FROM 'regress x2 x1' ARE NOT RANDOMLY DISTRIBUTED, WHICH THEY WOULD BE IF X1 AND X2 WERE NOT CORRELATED.

regress y res_x2_x1 
eststo m3

* THE NON-RANDOM PATTERN MEANS THAT INFORMATION FROM THE OMITTED VARIABLE IS "LEAKING" INTO THE RESIDUALS OF X1.
* THUS, OUR ESTIMATE IS UPWARD BIASED (OVERESTIMATING THE EFFECT OF X1 ON Y) WHEN WE OMIT X2.

regress y x1 // SHORT
eststo m1
regress y x1 x2 // LONG
eststo m2
esttab m1 m2 m3
* IN MODEL 3, THE COEFFICIENT OF THE RESIDUAL TERM IS EXACTLY THE MAGNITUDE OF THE COEFFICIENT OF X2 IN MODEL 2. 
* THIS IS BECAUSE THE RESIDUALS CAPTURE THE LINK BETWEEN X2 AND Y.


***MULTIVARIATE OLS REGRESSION***

* IN THE ABSENCE OF EXPERIMENTAL DATA, WE OFTEN RELY ON A CONTROL STRATEGY.

use NHIS2009_clean_subsample2.dta, clear

* ASSIGNMENT TO TREATMENT (=HEALTH INSURANCE COVERAGE) IS DETERMINED BY EMPLOYMENT, INCOME, EDUCATION, ETC. + A RANDOM COMPONENT.
* WE AIM AT ISOLATING THE RANDOM COMPONENT, HOLDING THE OTHER DETERMINANTS CONSTANT ACROSS UNTREATED/TREATED GROUPS.
* THIS MEANS THE CIA IS FULFILLED.
* THEN, THE DIFFERENCE IN HEALTH OUTCOMES ACROSS UNTERATED/TREATED GROUPS IS THE ESTIMATED CAUSAL EFFECT OF TREATMENT.

* LET'S RUN SOME MULTIVARIATE REGRESSIONS/SENSITIVITY ANALYSIS AND COMPARE WITH OUR "BARE BONES" REGRESSION MODEL.
* SENSITIVITY ANALYSIS IS DONE TO SEE IF ASSIGNMENT TO TREATMENT IS RANDOMIZED - OR AS GOOD AS RANDOM.

gen ln_inc=ln(inc)
label variable ln_inc "Log of family income"
* FIRST, WE LOG-TRANSFORM THE FAMILY INCOME VARIABLE (IN STATA, LN STANDS FOR NATURAL LOGARITHM).
* THIS MAKES INTERPRETATION MORE MEANINGFUL (ONE PERCENT INCREASE IN FAMILY INCOME ASSOCIATED WITH AN INCREASE OF BETA/100 UNITS OF Y).

reg hlth hi [aw=perweight], robust
eststo m1
reg hlth hi fml age famsize [aw=perweight], robust
eststo m2
reg hlth hi fml age famsize nwhite [aw=perweight], robust
eststo m3
reg hlth hi fml age famsize nwhite yedu [aw=perweight], robust
eststo m4
reg hlth hi fml age famsize nwhite yedu empl [aw=perweight], robust
eststo m5
reg hlth hi fml age famsize nwhite yedu empl ln_inc [aw=perweight], robust
eststo m6
esttab m1 m2 m3 m4 m5 m6, b(%7.2f) se(%7.2f) r2(%7.2f) label
* THE OPTIONS SPECIFY THAT WE WANT TO SEE THE COEFFICINENTS, STANDARD ERRORS (DEFAULT IS T-STATISTIC), AND R-SQUARED, WITH TWO DECIMALS SHOWN.
* WE ALSO SPECIFY THAT WE WOULD LIKE STATA TO SHOW VARIABLE LABELS IN PLACE OF VARIABLE NAMES.
* TYPE 'help esttab' TO READ ABOUT PRODUCING TABLES WITH THIS COMMAND.
   
* COMPARING THE MODELS, WE SEE THAT ADDING CONTROL VARIABLES LEADS TO ATTENUATION OF THE ESTIMATED COEFFICIENT FOR HEALTH INSURANCE.
* WHY SO?
* THE LIKELIHOOD OF HAVING HEALTH INSURANCE IS CORRELATED WITH EDUCATION, EMPLOYMENT STATUS, AND FAMILY INCOME.
* THUS, BACKGROUND PLAYS AN IMPORTANT ROLE FOR WHETHER SOMEONE HAS HEALTH INSURANCE COVERAGE OR NOT.

* ALTERNATIVELY, WE CAN CHECK FOR WHETHER ASSIGNMENT TO TREATMENT MIGHT HAVE BEEN RANDOM OR NOT IN THE FOLLOWING WAY.

reg hi fml age famsize nwhite yedu empl ln_inc [aw=perweight], robust
* WHAT WOULD THE RESULT BE IF ASSIGNMENT TO TREATMENT WAS RANDOM?


* USING A CONTROL STRATEGY, HAVE WE SATISFACTORILY ACCOUNTED FOR OVB?
* CAN WE CONCLUDE THAT HEALTH INSURANCE HAS NO EFFECT ON HEALTH STATUS? 
* WHY? WHY NOT? 
* SELECTION MAY LEAD TO US TRYING TO COMPARE APPLES AND ORANGES (=VERY DIFFERENT PEOPLE SELECT INTO HAVING INSURANCE).

* CIA HOLDS IF WE INCLUDE ALL VARIABLES THAT ARE CORRELATED WITH BOTH TREATMENT AND HEALTH STATUS IN THE MODEL.
* IS THERE A WAY TO TEST FOR OVB ORIGINATING FROM VARIABLES THAT WE DO NOT HAVE ACCESS TO? 
* NO! WE HAVE TO MAKE AN ARGUMENT FOR WHETHER THERE ARE SUCH CONFOUNDERS OR NOT.


***BAD CONTROL PROBLEM***

* SO WHY NOT JUST ADD ALL POSSIBLE CONTROL VARIABLES INTO THE MODEL TO TRY TO REMOVE OVB?
* NOT ALL CONTROLS ARE GOOD.

* BAD CONTROL IS A VARIABLE THAT IS ITSELF INFLUENCED BY THE TREATMENT.

use psidextract.dta, clear
* THIS IS A MODIFIED EXTRACT OF A PSID DATASET ON WAGES 1976-1982 (BALTAGI & KHANTI-AKOM 1990).

* LET'S EXAMINE THE EFFECT OF A COLLEGE DEGREE ON EARNINGS.
* THERE ARE BLUE AND WHITE COLLAR WORKERS - OCCUPATION.
* OCCUPATION IS RELATED TO BOTH EDUCATION AND EARNINGS, BUT SHOULD WE INCLDE IT IN OUR MODEL AS A CONTROL OR NOT?

* HERE, WE HAVE A REGRESSION MODEL WITH WAGE AS THE OUTCOME AND HAVING A COLLEGE DEGREE (0-1) AS THE MAIN EXPLANATORY VARIABLE.
* WE ALSO HAVE INFORMATION ON WHETHER THE PERSON WORKS IN BLUE (occ==1) OR WHITE COLLAR (occ==0) SECTOR.

reg lwage coll, cl(id)
eststo m1
reg lwage coll fem blk, cl(id)
eststo m2
reg lwage coll fem blk south smsa, cl(id)
eststo m3
reg lwage coll fem blk south smsa occ, cl(id)
eststo m4
esttab m1 m2 m3 m4, b(%7.2f) se(%7.2f) r2(%7.2f)

* ADDING THE BAD CONTROL INTO THE REGRESSION MODEL ATTENUATES THE ESTIMATED COEFFICIENT OF COLLEGE.

pwcorr coll occ, star(.05)
* WE SEE THERE IS A STRONG CORRELATION BETWEEN COLLEGE AND OCCUPATION, BECAUSE OCCUPATION IS AN OUTCOME OF EDUCATION.

* WHAT IS THE INTERPRETATION OF THE COEFFICIENTS OF 'coll' AND 'occ' IN MODEL 6?
* THE BASE CATEGORY HERE IS NO COLLEGE, WHITE COLLAR. SO THE COEFFICIENT OF 'coll' IS THE INCREASE IN EARNINGS 
* FROM COLLEGE, CONDITIONAL ON WORKING IN A WHITE-COLLAR SECTOR.
* THE COEFFICIENT OF 'occ' IS THE DECREASE IN EARNINGS WHEN WORKING IN A BLUE-COLLAR SECTOR, CONDITIONAL ON NO COLLEGE.
* NOTE HOW THE ESTIMATES REFLECT THE UNDERLYING ABILITY OF THOSE WHO WORK IN WHITE-COLLAR JOBS WITHOUT HAVING COLLEGE EDUCATION.
* THIS IS A RESULT IN ITSELF, BUT IT IS NOT EXACTLY ANSWERING OUR RESEARCH QUESTION. 
* ALWAYS THINK ABOUT WHAT YOUR CONTROL VARIABLES ARE ACTUALLY CAPTURING. 


* SO HOW TO DEFINE A REGRESSION MODEL? 
* DRAW ON INSIGHTS FROM ECONOMIC THEORY: WHAT VARIABLES DOES THEORY SAY ARE IMPORTANT?
* USUALLY VARIABLES MEASURED BEFORE ASSIGNMENT TO TREATMENT ARE GOOD CONTROLS


* DRAWING CAUSAL INFERENCE WHILE RELYING ON A CONTROL STRATEGY IS THREATENED BY THE CIA NOT HOLDING.
* IN OTHER WORDS, WE HAVE TO THINK ABOUT WHETHER OUR CONTROL VARIABLES ARE REALLY CAPTURING ALL CONFOUNDERS.
* WE HAVE TO MAKE A CASE FOR WHETHER WE THINK THE ASSUMPTION HOLDS OR NOT, AND WHAT IT IMPLIES FOR OUR ESTIMATES (=ARE THEY REALLY CAUSAL OR NOT).
* DOES THE OVB BIAS OUR ESTIMATES UPWARDS OR DOWNWARDS?

log close

clear

* SINCE THE CONTROL STRATEGY HAS ITS PITFALLS WITH REGARDS TO OVB, IT CAN BE CHALLENGING TO MAKE A CASE FOR OUR ESTIMATES BEING CAUSAL.
* WE WOULD RATHER WORK WITH DATA THAT AT LEAST PARTLY MEETS THE CRITERIA OF RANDOM ASSIGNMENT TO TREATMENT.
* NEXT TIME, WE EXAMINE DATA FROM "NATURAL" EXPERIMENTS OR QUASI-EXPERIMENTS AND METHODS USED TO EXPLOIT SUCH DATA.
