***************************************************
*                  EKHM66 Fall 2020               *
*          Lab 3: Instrumental variables          *
***************************************************

/*TIP: EXECUTE COMMANDS FROM THE DO-FILE BY HIGHLIGHTING THE COMMANDS AND PRESSING CTRL+D.
PRESS THE PAGE UP KEY TO RETRIEVE THE MOST RECENTLY EXECUTED COMMAND TO THE COMMAND WINDOW.*/

* LAST TIME WE EXAMINED THE PITFALLS OF OLS REGRESSION AND THE CONTROL STRATEGY WHEN ESTIMATING CAUSAL EFFECTS.
* SINCE EXPERIMENTAL DATA ARE HARD TO FIND, THE NEXT BEST THING IS DATA THAT AT LEAST PARTLY MEET THE CRITERIA OF RANDOM ASSIGNMENT TO TREATMENT.
* NOW WE EXAMINE QUASI-EXPERIMENTAL DATA AND METHODS.
* THE FIRST ONE IS THE INSTRUMENTAL VARIABLES METHOD.

*REQUIRED READING FOR THIS LAB IS ANGRIST AND KRUEGER (1991): DOES COMPULSORY SCHOOL ATTENDANCE AFFECT SCHOOLING AND EARNINGS?

***SETTING THE WORKING DIRECTORY***

cd "\\uwfpcluster01.uw.lu.se\el8285la$\Documents\PhD programme\Teaching\EKHM66 Econometrics II, Ht 2020\Labs\Lab 3 (Instrumental variables)"


***STARTING A LOG FILE***

log using "lab 3.log", replace


* IN THIS SECTION, WE FOLLOW ANGRIST AND KRUEGER'S (1991) CLASSIC PAPER WITHIN INSTRUMENTAL VARIABLE ANALYSIS.
* THEY LOOK AT THE AGE-OLD QUESTION OF THE EFFECTS OF YEARS OF EDUCATION ON EARNINGS.
* THE IDEA IS TO ADDRESS SELECTION INTO SCHOOLING BY EXPLOITING TIMING OF BIRTH COMBINED WITH AN ARBITRARY RULE IN THE US SCHOOL SYSTEM.
* THE SELECTION ISSUE IS THAT WE CANNOT CONTROL FOR UNOBSERVABLE FACTORS 
* THAT MIGHT DETERMINE BOTH HOW MANY YEARS A PERSON STUDIES AND THE SUBSEQUENT EARNINGS (USUALLY CALLED "ABILITY").

* THE MECHANISM BEHIND THE INSTRUMENT IS THAT AMONG INDIVIDUALS BORN IN THE SAME YEAR, 
* LATER-BORN STUDENTS ENTER SCHOOL YOUNGER THAN THOSE BORN EARLY IN THE YEAR.
* THEY HAVE TO FINISH MORE SCHOOLING BEFORE THEY ARE ALLOWED TO DROP OUT.


***IMPORTING DATA***

use ak91.dta, clear

* FIRST, LET'S TAKE A LOOK AT THE DATA.
br
* WAGE HAS BEEN LOG-TRANSFORMED, EDUCATION IS IN YEARS, BUT BIRTH INFORMATION IS NON-INTUITIVE.

* WE WANT TO INPUT INFORMATION ON TIME OF BIRTH, YEAR AND QUARTER, IN A FORMAT THAT STATA UNDERSTANDS. 
* SEE FOR EXAMPLE https://stats.idre.ucla.edu/stata/modules/using-dates-in-stata/ IF YOU NEED TO WORK WITH DATES,
* FOR EXAMPLE WHEN WRITING YOUR THESIS.

gen yqob = yq(1900 + yob, qob)
* THIS IS A TIME OF BIRTH (YEAR-QUARTER) THAT STATA UNDERSTANDS, HOWEVER IT IS NOT UNDERSTANDABLE FOR US.
format yqob %tq
* WE HAVE TO FORMAT THE NEW TIME OF BIRTH VARIABLE.
* YQ AND TQ ARE THE "QUARTERLY" TIME FORMAT USED IN STATA (https://www.stata.com/manuals13/u24.pdf).
label variable yqob "Year and quarter of birth"

* CREATE A BINARY INSTRUMENT FOR QUARTER OF BIRTH.
gen z=.
replace z=0  if qob>=1 & qob<=3
replace z=1 if qob==4

label variable z "QOB instrument"
label define zlabel 0 "Born 1st-2nd-3rd quarter of year" 1 "Born 4th quarter of year"
label values z zlabel
* WE EXPECT QUARTER OF BIRTH TO BE A RANDOMLY ASSIGNED PREDICTOR OF EDUCATIONAL ATTAINMENT.
* WE EXPECT IT TO BE UNCORRELATED WITH EARNIGS, OTHER THAN THROUGH ITS IMPACT ON REQUIRED EDUCATIONAL ATTAINMENT.

* LET'S TAKE A LOOK AT OUR INSTRUMENT.
tab qob z


***LATE ASSUMPTIONS***

* UNDER RESONABLE ASSUMPTIONS, IV CAPTURES THE EFFECT OF TREATMENT ON COMPLIERS.
* THIS IS CALLED THE LOCAL AVERAGE TREATMENT EFFECT (LATE).

* DID EVERYONE REACT TO THE INSTRUMENT THE SAME WAY?
* PROBABLY NOT. THUS, WE EXPECT HETEROGENEOUS OUTCOMES AMONG THOSE BORN IN THE FOURTH QUARTER:
	* COMPLIERS = STAYED IN SCHOOL LONGER DUE TO LEGAL REQUIREMENT.
	* ALWAYS-TAKERS = WOULD HAVE STAYED IN SCHOOL IN ANY CASE.
	* NEVER-TAKERS = WOULD NOT HAVE STAYED IN ANY CASE.
	* DEFIERS = DROPPED OUT OF SCHOOL EARLIER TO DEFY THE LEGAL REQUIREMENT - MONOTONICITY ASSUMPTION RULES THIS GROUP OUT!
	
* LATE ASSUMPTIONS:
	*1. STRONG FIRST STAGE
	*2. INDEPENDENCE ASSUMPTION
	*3. EXCLUSION RESTRICTION
	*4. MONOTONICITY (NO "DEFIERS", P. 113 IN THE BOOK).

	
* 1. STRONG FIRST STAGE

* THIS MEANS THAT THE INSTRUMENT HAS A CLEAR EFFECT ON ASSIGNMENT TO TREATMENT.
* IF NOT, THE INSTRUMENT DOES NOT HELP US IN GETTING RID OF PROBLEMATIC VARIATION IN EARNINGS.
* THE CHALLENGE WITH IV IS USUALLY FINDING VALID INSTRUMENTS.


* FIRST STAGE AND REDUCED FORM VISUALLY

* LET'S GET AN IDEA OF WHETHER QUARTER OF BIRTH AND EDUCATION ARE RELATED TO EACH OTHER.
* WE REPRODUCE THE INFORMATION PRESENTED IN TABLE 6.4 IN THE BOOK.

tab z, sum(s) // ALL YEARS
tab yqob, sum(s) // EACH YEAR AND QUARTER SEPARATELY.
* DOES IT LOOK LIKE THERE MAY BE DIFFERENCES IN EDUCATIONAL ATTAINMENT BY BIRTH QUARTER?

* THE REDUCED FORM IS THE EFFECT OF THE INSTRUMENT ON THE OUTCOME. 
* IT REPRESENTS THE DIFFERENCE IN EARNINGS BETWEEN THOSE WHO WERE BORN IN THE FOURTH QUARTER AND THOSE WHO WERE NOT.
* IF WE DON'T HAVE AN EFFECT, THE STUDY WILL BE MEANINGLESS (=QOB DOES NOT MATTER FOR EARNINGS).

* WE CAN ASSESS THIS VISUALLY BY TAKING SUMMARY STATISTICS OF EARNINGS BY BIRTH QUARTER.
tab z, sum(lnw)
tab yqob, sum(lnw)

* THE FIRST STAGE AND REDUCED FORM MIGHT BE EASIER TO GRASP BY GRAPHING RATHER THAN IN TABLES. 
* WE NEED TO CONSTRUCT THE MEAN EDUCATION (FIRST STAGE) AND MEAN WAGE (REDUCED FORM) PER YEAR AND QUARTER.
* IN OTHER WORDS, WE NEED TO BOIL THE WHOLE DATASET DOWN TO THE MEANS FOR GRAPHING.
* WE DO THIS WITH THE 'collapse' COMMAND. IT CREATES A NEW DATASET WITH ONLY THE MEANS, GROUPED BY THE INSTRUMENT.

* STATA CAN ONLY HAVE ONE DATASET LOADED AT A TIME.
* AFTER DOING THE GRAPHS, WE NEED THE INDIVIDUAL DATA AGAIN FOR MODELLING AND HAVE TO LOAD IT ONE MORE TIME. 
* THERE ARE THE COMMANDS preserve AND restore THAT CAN BE USED IF YOU DO NOT WANT TO COLLAPSE THE WHOLE TABLE, BUT WE WILL NOT USE THEM IN THIS LAB.

collapse (mean) s (mean) lnw (mean) qob, by(yqob)
* WE INCLUDE qob BECAUSE WE WANT TO TO KEEP IT FOR LABELLING PURPOSES (OTHERWISE STATA WILL DROP IT).
* NOW, OUR VARIABLES ARE ONLY MEANS (40 OBSERVATIONS INSTEAD OF 300.000)
br
* NOW WE CAN GRAPH EDUCATION BY QOB:
graph twoway line s yqob
* twoway SPECIFIES TWO VARIABLES, line SPECIFIES THAT DATA POINTS SHOULD BE CONNECTED WITH A LINE.

* WE CAN ADD THE QUARTERS AS DOTS WITH LABELS (THIS CORRESPONDS TO FIGURE 6.1, P. 230 IN ANGRIST & PISCHKE)
graph twoway (line s yqob) (scatter s yqob, mlabel(qob) msize(small) msymbol(S))
* scatter SPECIFIES THE DOTS.
* mlabel, msize, AND msymbol REFER TO THE MARKER LABEL, SIZE, AND SHAPE.
* TYPE 'help graph' TO LEARN MORE ABOUT DIFFERENT OPTIONS.

* THIS GRAPH IS A VISUAL REPRESENTATION OF THE "FIRST STAGE": THE INSTRUMENT MUST HAVE AN EFFECT ON THE CAUSAL VARIABLE OF INTEREST.
* AGAIN, IF QOB DOES NOT AFFECT SCHOOLING, WE HAVE A WEAK INSTRUMENT THAT WE CANNOT USE.
* APART FROM THE LATER QOB:S GETTING MORE EDUCATION THAT THE EARLIER, WE CAN ALSO NOTICE THAT EDUCATION GENERALLY INCREASES AS AMERICAN BIRTH COHORTS PROGRESS.

* THE FIRST STAGE REPRESENTS THE "WELL-BEHAVED" PART OF THE VARIATION.
* IT IS THE VARIATION IN EDUCATIONAL ATTAINMENT THAT IS ATTRIBUTED TO QOB, NOT TO ABILITY OR ANYTHING ELSE.
* IT IS THUS FREE FROM SELECTION BIAS, AND ACTING AS A QUASI-EXPERIMENT.

* REMEMBER THAT GRAPHING IS NOT A FORMAL TEST. 
* FORMAL TESTS SHOULD ALWAYS BE DONE TO DETERMINE FIRST STAGE STRENGTH.

* WE NEED TO RESTORE THE ORIGINAL DATA AFTER USING 'collapse'.
use ak91.dta, clear

* WE ALSO NEED TO RECREATE THE VARIABLES BECAUSE WE HAD NOT SAVED THE DATA PRIOR TO USING 'collapse'.
* THIS IS JUST THE SAME CODE AS IN THE START OF THE LAB.
gen yqob = yq(1900 + yob, qob)
format yqob %tq
label variable yqob "Year and quarter of birth"
gen z=.
replace z=0  if qob>=1 & qob<=3
replace z=1 if qob==4
label variable z "QOB instrument"
label define zlabel 0 "Born 1st-2nd-3rd quarter of year" 1 "Born 4th quarter of year"
label values z zlabel

* FORMAL TEST OF THE FIRST STAGE
reg s z, r
* 'r' IS SHORT FOR 'robust'.
* BEING BORN IN THE FOURTH QUARTER IS POSITIVELY ASSOCIATED WITH EDUCATIONAL ATTAINMENT. 
* THE STRONGER THE ASSOCIATION BETWEEN THE INSTRUMENT(S) AND THE TREATMENT, THE STRONGER THE IDENTIFICATION OF THE MODEL.
* WEAK INSTRUMENTS LEAD TO IMPRECISE AND POTENTIALLY MISLEADING ESTIMATES (SEE CHAPTER 6 IN CAMERON & TRIVEDI 2009).
* WHAT IS CONSIDERED A "STRONG" FIRST STAGE? 
* AS A RULE-OF-THUMB, THE F-STATISTIC OF THE FIRST STAGE OF 2SLS SHOULD EXCEED 10 (STOCK, WRIGHT & YOGO 2002).

* FORMAL TEST OF THE REDUCED FORM
reg lnw z, r
* WE SEE AN EFFECT FROM THE INSTRUMENT ON THE OUTCOME (NO NEED TO INTERPRET THE MAGNITUDE AT THIS STAGE).
* THIS MEANS THAT QOB IS CAPTURING SOMETHING THAT MATTERS FOR EARNINGS.
* IF THE EFFECT HAD BEEN ZERO, THE IV ANALYSIS WOULD BE POINTLESS.
* WE DO NOT NECESSARILY HAVE TO TEST THE REDUCED FORM, FIRST STAGE IS THE IMPORTANT STEP TO TEST.
* BUT IT MAY BE A GOOD IDEA TO CHECK ANYWAY.

* IF THERE WERE NO STATISTICALLY SIGNIFICANT DIFFERENCES IN MEANS OF THE OUTCOME AND CAUSAL VARIABLE OF INTEREST WHEN USING THE INSTRUMENT,
* WE MIGHT BE WORRIED THAT WHAT WE OBSERVED IN THE GRAPHS WAS MISLEADING.
* BUT NOW WE CAN MOVE FORWARD WITH THE IV ANALYSIS.


* 2. INDEPENDENCE ASSUMPTION

* WE MUST MAKE SURE THAT OUR INSTRUMENT REALLY IS AS GOOD AS RANDOMLY ASSIGNED. 
* IN OTHER WORDS, QUARTER OF BIRTH HAS TO BE UNCORRELATED WITH ALL UNOBSERVED DETERMINANTS OF EARNINGS (CONDITIONAL ON COVARIATES).
* THERE IS NO FORMAL WAY TO TEST THIS USING THE CURRENT DATA.
* FOR EACH IV MODEL, THERE NEEDS TO BE THOROUGH REASONING AROUND POTENTIAL PATHWAYS OF THE INSTRUMENT NOT BEING INDEPENDENT OF UNOBSERVABLES.
* A DISCUSSION ON A POTENTIAL EFFECT FROM MOTHER'S EDUCATION ON QOB CAN BE READ ON PAGE 233 OF ANGRIST & PISCHKE.
* A NAIVE WAY OF TESTING THIS ASSUMPTION WOULD BE TO TRY A BALANCE CHECK ON CO-VARIATES TO SEE IF IT LOOKS RANDOM (LIKE IN LAB 1).
* HOWEVER, THIS DOES NOT RULE OUT DEPENDENCE ON UNOBSERVABLE VARIABLES.


* 3. EXCLUSION RESTRICTION

* WE ASSUME THAT THE INSTRUMENT ONLY AFFECTS THE OUTCOME VIA OUR CAUSAL VARIABLE OF INTEREST. 
* THAT MEANS:
* QOB CAN ONLY AFFECT WAGES THROUGH YEARS OF EDUCATION. 
* IF THERE WERE ANY OTHER CHANNEL, THE INSTRUMENT WILL NOT BE NOT EXCLUSIVE AND WE WILL GET BIASED RESULTS.
* THERE IS NO WAY OF TESTING THIS!
* IN THE CASE OF THE CURRENT DATA, AN EXAMPLE OF A DISCUSSION AROUND THE EXCLUSION RESTRICTION CAN BE FOUND ON PAGE 234 IN THE BOOK.
* THE POTENTIAL PROBLEM IS THAT YOUNGER STUDENTS WOULD STRUGGLE IN CLASS JUST FOR THE SAKE OF BEING YOUNGER.
* THEN, QOB WOULD HAVE A NEGATIVE EFFECT ON SCHOOLING INDEPENDENTLY OF THE INSTRUMENT.
* IN THE BOOK, THE ARGUMENT IS THAT THIS IS NOT A PROBLEM.


* 4. MONOTONICITY

* THIS ASSUMPTIONS STATES THAT THOSE INFLUENCED BY THE INSTRUMENT MUST ALL BE INFLUENCED IN THE SAME WAY.
* I.E. BEING BORN IN THE FOURTH QUARTER CAN ONLY MAKE STUDENTS STAY IN SCHOOL LONGER, NOT DROP OUT EARLIER.
* IF THE MONOTONICITY ASSUMPTION IS VIOLATED, OUR ESTIMATES OF THE TREATMENT EFFECT MAY BE WASHED OUT,
* THE EFFECT ON DEFIERS PARTLY OF FULLY CANCELS OUT THE EFFECT ON COMPLIERS. 

* THERE ARE SOME METHODS TO TEST FOR MONOTONICITY, WHICH WE DO NOT GO INTO IN THIS COURSE.
* TO TEST IT, WE WOULD NEED INFORMATION ON HOW PEOPLE REACTED TO THE INSTRUMENT.
* HOW WOULD WE DISTINGUISH NEVER-TAKERS FROM THE POTENTIAL DEFIERS?
* TO SIMPLIFY LIFE, WE CAN JUST ASSUME MONOTONICITY, AND INCLUDE A DISCUSSION ON HOW PLAUSIBLE THE ASSUMPTION IS,
* AND WHAT THE IMPLICATIONS ARE FOR OUR ESTIMATES IF THE ASSUMPTION DOES NOT HOLD.


***IV MODELLING***

* FIRST, WE CAN RUN A "NAIVE" OLS REGRESSION JUST TO SEE WHAT THE ESTIMATES LOOK LIKE.
* THIS CORRESPONDS TO COLUMN 1, TABLE 6.5 IN ANGRIST & PISHCKE.

reg lnw s, r
* THERE IS A POSITIVE ASSOCIATION BETWEEN EDUCATION AND EARNINGS, AS EXPECTED. 
* THIS IS A SIMPLIFIED MODEL WITHOUT ANY CONTROL VARIABLES. 
* THERE ARE OF COURSE OBSERVABLES SUCH AS GENDER, ETHNICITY, ETC. THAT COULD BE INCLUDED IN A FULL MODEL.
* HOWEVER, AS POINTED OUT BY THE AUTHORS, THIS MODEL IS PROBLEMATIC AND SUFFERS FROM OMITTED VARIABLE BIAS (UNOBSERVABLE ABILITY).
* THIS OVB CANNOT BE CONTROLLED AWAY. 


***IV MANUALLY***

* FIRST STAGE:
reg s z, r

* REDUCED FORM:
reg lnw z, r

* COMBINE THE TWO TO MANUALLY OBTAIN THE IV ESTIMATE (REDUCED FORM DIVIDED BY FIRST STAGE):
display .0068132 / .0921209
* STATA HAS A CALCULATOR FUNCTION BUILT IN, TYPE 'help display' TO FIND OUT MORE.
* THE ESTIMATED INCREASE FROM AN ADDITIONAL YEAR IN SCHOOL ON THE EARNINGS OF COMPLIERS (LATE) IS 7.4 %. 

* IF THE FIRST-STAGE COEFFICIENT WAS 0 WE WOULD GET NO ESTIMATE SINCE IT IS IN THE DENOMINATOR. 
* IF THE REDUCED FORM IS 0, IV ESTIMATE WILL BE 0.

* THE PROBLEM WITH MANUALLY CALCULATING THE IV ESTIMATE IS THAT WE UNDERESTIMATE STANDARD ERRORS.
* OR, IN THIS CASE, WE DO NOT EVEN OBTAIN STANDARD ERRORS BECAUSE WE DID THE CALCULATION BY HAND.
* SO, WE HAVE NO IDEA OF HOW UNCERTAIN THE ESTIMATE IS.
* THE STANDARD ERRORS OF THE IV ESTIMATE NEED TO ACCOUNT FOR THE DOUBLE UNCERTAINTY ASSOCIATED WITH ESTIMATION IN TWO STEPS.


***TWO-STAGE LEAST SQUARES (2SLS)***

* THE 'ivregress' COMMAND PRODUCES THE IV ESTIMATE IN ONE STEP.
* IT NEEDS AN ESTIMATOR TO BE SPECIFIED (2SLS, LIML, GMM).
* WE USE 2SLS, IT GIVES THE SAME RESULT AS THE HAND CALCULATION ABOVE.

ivregress 2sls lnw (s = z), first

* THIS CORRESPONDS TO COLUMN 2 IN TABLE 6.5.
* THE 'first' OPTION PRODUCES STANDARD ERRORS THAT ACCOUNT FOR UNCERTAINTY ORIGINATING FROM BOTH ESTIMATION STEPS
* IT ALSO YIELDS DETAILED OUTPUT ON THE FIRST STAGE REGRESSION.


***IV ESTIMATE WITH MORE THAN ONE INSTRUMENT AND/OR COVARIATES***

* THE IV ESTIMATE ABOVE IS FOR THE SIMPLE CASE OF ONE INSTRUMENT AND NO OTHER COVARIATES.
* WE USED THIS EXAMPLE BECAUSE IT IS USEFUL FOR LEARNING THE INTUITION BEHIND IV.
* IF YOU HAVE COVARIATES AND MORE THAN ONE INSTRUMENT, 'ivregress 2sls' CAN HANDLE THIS.
* TYPE 'help ivregress' TO FIND OUT MORE.

* FOR EXAMPLE, IF WE HAD HYPOTHETICAL PANEL DATA ON INDIVIDUALS OF DIFFERENT GENDERS AND ETHNICITIES, 
* WE COULD SPECIFY A MODEL WHERE WE CONTROL FOR THESE FACTORS.
* WE CAN ALSO ADD MORE THAN ONE INSTRUMENTS IF AVAILABLE.

ivregress 2sls y gender ethnicity (x = instrument1 instrument2), vce(id)
* WE ADDED PERSON-CLUSTERED STANDARD ERRORS TO ACCOUNT FOR CORRELATION IN ERRORS ACROSS 
* OBSERVATIONS OF THE SAME PERSON AT DIFFERENT TIME POINTS.

* THE DIFFERENCE BETWEEN THE CALCULATION BY HAND AND THE 2SLS IS THAT 
* IN 2SLS, THE FIRST STAGE CONSISTS OF PREDICTED A VALUE FOR EACH OBSERVATION (THE "UNPROBLEMATIC VARIATION"). 
* THE PREDICTED "UNPROBLEMATIC" VALUES ARE THEN ADDED INTO THE SECOND STAGE EQUATION.

* WITH OVERIDENTIFIED MODELS (MORE THAN ONE INSTRUMENT) IT IS POSSIBLE TO CONDUCT A WEAK TEST OF THE EXCLUSION RESTRICTION.
* THIS WAS DISCUSSED IN THE LECTURE BUT WE WILL NOT GO INTO IT IN THIS LAB.


* AS SEEN ABOVE, RUNNING AN IV MODEL IS NOT PARTICULARLY COMPLICATED WHEN IT COMES TO THE CODING.
* IT IS, HOWEVER, HARD TO FIND A GOOD INSTRUMENT IN THE FIRST PLACE.
* A LOT OF THEORETICAL WORK IS NEEDED IN ORDER TO ARGUE THAT A PROPOSED INSTRUMENT IS VALID.
* THE CASE ABOVE IS THE SIMPLEST POSSIBLE, BUT THERE IS A LARGE LITERATURE ON 2SLS AND WHEN TO USE IT AND NOT TO USE IT.
* AN INCORRECTLY SPECIFIED INSTRUMENT CAN LEAD TO BIASES THAT ARE LARGER AND MORE PROBLEMATIC THAN OLS SUFFERING FROM OVB.

* SINCE GOOD INSTRUMENTS ARE HARD TO FIND, WE NEXT TURN TO OTHER QUASI-EXPERIMENTAL METHODS.

log close
clear
