***************************************************
*              EKHM66 Fall 2020                   *
*     Lab 4: Regression discontinuity designs     *
***************************************************

/*TIP: EXECUTE COMMANDS FROM THE DO-FILE BY HIGHLIGHTING THE COMMANDS AND PRESSING CTRL+D.
PRESS THE PAGE UP KEY TO RETRIEVE THE MOST RECENTLY EXECUTED COMMAND TO THE COMMAND WINDOW.*/

* LAST TIME, WE APPLIED INSTRUMENTAL VARIABLES REGRESSION TO SOLVE THE PROBLEM OF SELECTION INTO TREATMENT.
* FINDING GOOD INSTRUMENTS IS HARD, HOWEVER.

* A REGRESSION DISCONTINUITY (RD) DESIGN EXPLOITS ARBITRARY RULES AND THRESHOLDS IN ORDER TO FIND "AS GOOD AS RANDOM" SITUATIONS TO GET RID OF BIAS. 
* RD DESIGNS CAN BE EITHER SHARP (TREATMENT SWITCHES ON/OFF AT A GIVEN VALUE) OR FUZZY (PROBABILITY/INTENSITY OF TREATMENT JUMPS AT A THRESHOLD - SAME AS A 2SLS IV DESIGN)

* IN THIS EXAMPLE, WE LOOK AT DATA ON MORTALITY CLOSE TO THE 21ST BIRTHDAY, WHICH IS THE LEGAL DRINKING AGE IN THE US.
* THE RD DESIGN IN THIS LAB IS A SHARP RD (I.E. ONE THRESHOLD THAT FLIPS TREATMENT ON/OFF). 

* THE CAUSAL QUESTION WE ASK IS: DOES ALCOHOL CONSUMPTION INCREASE THE MORTALITY OF YOUNG ADULTS?
* THE CONFOUNDING FACTOR IS THAT DRINKING BEHAVIOUR MAY BE CORRELATED WITH GENERAL RISK BEHAVIOUR.


* CARPENTER AND DOBKIN (P.164-165):
* "Providing precise estimates of the strength of the causal relationship between alcohol consumption and 
* mortality is complicated by the usual problem of unobserved heterogeneity among individuals that is likely 
* related to alcohol consumption and to the determinants of mortality."

* THE MINIMUM LEGAL DRINKING AGE (MLDA) CAN BE SEEN AS A QUASI-EXPERIMENT WHEREBY DRINKING IS ASSUMED TO HAPPEN ON ONE SIDE OF THE THRESHOLD, BUT NOT THE OTHER.
* THE UNOBSERVED AND OBSERVED VARIABLES THAT ALSO DETERMINE MORTALITY ARE LIKELY TO BE SMOOTH ACROSS THE 21-YEAR THRESHOLD.
* DRINKING MIGHT INCREASE HEAVILY AT THE THRESHOLD.
* THEN, THE CHANGE IN DRINKING SHOULD BE THE ONLY VARIATION IN BEHAVIOUR AT THE 21ST BIRTHDAY.
* WE CAN ATTRIBUTE CHANGES IN MORTALITY AROUND THE THRESHOLD TO CHANGES IN DRINKING.

* WE WORK WITH CARPENTER AND DOBKIN'S DATA.
* THE DATA CONTAIN AGGREGATE DEATH RATES (PER 100.000) GROUPED INTO A VERY SPECIFIC AGE RANGE (19-23). 
* DEATH RATES ARE AVAILABLE BY DIFFERENT CAUSES OF DEATH, SOME MORE THEORETICALLY RELATED TO ALCOHOL THAN OTHERS.


***SETTING THE WORKING DIRECTORY***

cd "\\uwfpcluster01.uw.lu.se\el8285la$\Documents\PhD programme\Teaching\EKHM66 Econometrics II, Ht 2020\Labs\Lab 4 (Regression discontinuity designs)"


***STARTING A LOG FILE***

log using "lab 4.log", replace


***LOADING DATA***

use AEJfigs, clear


* FIRST, LET'S EXPLORE THE DATA.
br
* THE AGE VARIABLE IS LINEAR AND THE NUMBER OF DAYS/MONTHS ARE EXPRESSED AS DECIMALS.


* WE NEED TO EXAMINE OUR "RUNNING VARIABLE" (AGE) AND HOW IT RELATES TO OUR OUTCOMES.
* AN RD DESIGN REQUIRES IN-DEPTH KNOWLEDGE ON THE POLICY/RULE/LAW THAT WE WANT TO EXPLOIT.
* WE NEED TO ARGUE THAT THE RUNNING VARIABLE COMBINED WITH THE ARBITRARY RULE CREATES VARIATION IN THE OUTCOME THAT IS AS GOOD AS RANDOM.
* WE START BY A VISUAL EXAMINATION OF THE DATA.


* ALL-CAUSE MORTALITY BY AGE (FIGURE 4.2, P.150).
twoway scatter all agecell


* AS THIS IS A SHARP RD DESIGN, WE CREATE A TREATMENT DUMMY.
gen over21=agecell>=21
label variable over21 "21st birthday cutoff"

gen centered_age=agecell-21 //HERE, WE CENTER THE AGE VARIABLE AROUND THE THRESHOLD.
label variable centered_age "Centered age"
* WE USE THE CENTERED AGE AS THE RUNNING VARIABLE BECAUSE IT IS EASIER TO INTERPRET THAN THE 

*LET'S INSPECT:
br agecell centered_age over21

* WE NEED TO PREDICT THE REGRESSION LINES ON EACH SIDE OF THE THRESHOLD SHOWN IN FIGURE 4.2. 
reg all centered_age over21 
* THIS FITS A REGRESSION FOR MORTALITY AS A FUNCTION OF AGE, CONTROLLING AWAY WHAT HAPPENS AFTER 21ST BIRTHDAY.
* IN OTHER WORDS, THIS FITS A REGRESSION LINE BASED ON THE EXPECTED TREND ON MORTALITY IF NOTHING CHANGES AT THE cutoff POINT.
predict allfit1
* HERE, 'predict' CALCULATES A LINEAR PREDICTION OF THE MORTALITY RATE BY AGE BASED ON THE REGRESSION WE JUST RAN.
* THE COMMAND STORE THE PREDICTED VALUES AS A NEW VARIABLE.
* WE USE THESE VALUES TO GRAPH MORTALITY BY AGE BEFORE TURNING 21.

gen over_bd=over21*centered_age // THIS IS A VARIABLE THAT IS ZERO BEFORE AND AGE AFTER THE cutoff. 
* IN OTHER WORDS, WE USE IT TO CAPTURE THE EXPECTED TREND IN MORTALITY STARTING AT THE 21ST BIRTHDAY.
label variable over_bd "Age*21st birthday cutoff"

reg all over21 over_bd 
* THIS FITS A REGRESSION FOR MORTALITY AS A FUNCTION OF AGE AFTER THE 21ST BIRTHDAY.
predict allfit2
* WE CALCULATE THE LINEAR PREDICTION.

* RECREATING FIGURE 4.2 WITH THE FITTED LINES:
twoway (scatter all agecell) (line allfit1 agecell if agecell<21) ///
                             (line allfit2 agecell if agecell>=21), ylabel(80(5)115)	
* THE FIGURE SHOWS ALL-CAUSE MORTALITY BY AGE.
* IN FACT, WE SPECIFY THREE GRAPHS IN THE SAME FIGURE.
* THE FIRST BIT CREATES A SCATTERPLOT OF THE ACTUAL MORTALITY RATES BY AGE.
* THE SECOND AND THIRD BITS CREATE LINE GRAPHS OF THE PREDICTED MORTALITY,
* ONE BEFORE AND ONE AFTER THE cutoff POINT.
* LASTLY, WE SPECIFY THE Y-AXIS TO SHOW RANGE 80 TO 115 AT 5-UNIT INTERVALS, LIKE IN THE BOOK.

							 
* IT SEEMS THAT THERE IS A CLEAR JUMP IN ALL-CAUSE MORTALITY WHEN THE RUNNING VARIABLE FLIPS OVER 21.
* BUT IS THIS A REAL DISCONTINUITY, OR ARE WE MISTAKING IT FOR NON-LINEARITY?
* SEE FIGURE 4.3 (P. 154) FOR EXAMPLES OF NON-LINEARITY VS. DISCONTINUITY.


* IT DOES SEEM LIKE WE HAVE A JUMP, BUT WE ALSO WANT TO FIT THE CORRECT FUNCTIONAL FORM TO OUR FUNCTION.
* AS EXPLAINED IN THE BOOK, THE DATA POINTS SUGGEST "MILD CURVATURE" (P. 155). 
* WE MIGHT WANT TO USE A SQUARED TERM FOR (CENTERED) AGE.
gen centered_age2 = centered_age^2
label variable centered_age2 "Centered age squared"

gen over_bd2 = over21*centered_age2
label variable over_bd2 "Age*21st birthday cutoff squared"
* GENERATING A VARIABLE TO CAPTURE THE SQUARED AGE AFTER THE 21ST BIRTHDAY.

* REGRESSIONS FOR FIGURE 4.4 (P. 158)
* QUADRATIC TERM ON EACH SIDE
reg all centered_age centered_age2 over21 
predict allfitq1
reg all over21 over_bd over_bd2
predict allfitq2

* FIGURE 4.4.	
twoway (scatter all agecell) (line allfit1 allfitq1 agecell if agecell<21,  lcolor(red black) lwidth(medthick medthick) lpattern(dash)) ///
                             (line allfit2 allfitq2 agecell if agecell>=21, lcolor(red black) lwidth(medthick medthick) lpattern(dash)), legend(off) ylabel(80(5)115)
* WE FIT THE LINEAR AND QUADRATIC PREDICTIONS ON TOP OF EACH OTHER FOR COMPARISON.
* YOU CAN DO THIS BY SIMPLY INCLUDING THE NAMES OF THE VARIABLES STORING THE PREDICTED VALUES WITHIN THE PARENTHESIS FOR THE LINE GRAPH. 
* YOU CAN PLAY AROUND WITH DIFFERENT SPECIFICATIONS TO SEE HOW THEY CHANGE THE APPEARANCE OF THE THE GRAPH.

* THE QUADRATIC FUNCTION SEEMS TO FIT THE DATA BETTER THAN THE LINEAR ONE.
* THE SHARP JUMP IS STILL CAPTURED BUT THE RECOVERY PATTERN OF MORTALITY RATES HAS A BETTER FIT.


* IN SUMMARY, IT SEEMS LIKE WE HAVE A CASE FOR USING RD. 
* BUT WHAT ABOUT DIFFERENT CAUSES OF DEATH?
* IF THE UNDERLYING CAUSAL MECHANISM IS THROUGH ALCOHOL-RELATED DEATHS, THEN THE RUNNING VARIABLE SHOULD IMPACT 
* FOR EXAMPLE MOTOR VEHICLE ACCIDENTS, BUT NOT DEATHS FROM CAUSES THAT ARE THEORETICALLY NOT RELATED TO ALCOHOL.
* WE CAN EXPLOIT THE DATA ON CAUSE OF DEATH AND USE THIS AS KIND OF A BALANCE CHECK, 
* TO SEE IF OUR TREATMENT APPEARS TO IMPACT EVEN CAUSES OF DEATH THAT SHOULD NOT BE RELATED TO THE PROPOSED MECHANISM.

						 
* THE MOTOR VEHICLE ACCIDENTS (MVA) DEATH DATA LOOK LIKE THIS:						 
twoway scatter mva agecell 

* LET'S REPRODUCE FIGURE 4.5 (P. 161), WHERE WE CONTRAST MVA MORTALITY TO "INTERNAL CAUSES",
* WHICH SHOULD NOT BE, AT LEAST IN A SHORT TIME PERIOD, DIRECTLY RELATED TO ALCOHOL CONSUMPTION.

* "MOTOR VEHICLE ACCIDENTS" ON LINEAR, AND QUADRATIC ON EACH SIDE
reg mva centered_age centered_age2 over_bd
predict mvfit1
reg mva over21 over_bd over_bd2
predict mvfit2

* "INTERNAL CAUSES" ON LINEAR, AND QUADRATIC ON EACH SIDE
reg internal centered_age centered_age2 over_bd
predict infit1
reg internal over21 over_bd over_bd2
predict infit2

twoway (scatter mva internal agecell) (line mvfit1 infit1 agecell if agecell<21) ///
                                      (line mvfit2 infit2 agecell if agecell>=21), ///
									   legend(off) text(28 20.1 "Motor vehicle fatalities") ///
									               text(17 22 "Deaths from internal causes")
		
* THERE IS NO JUMP IN DEATHS FROM "INTERNAL CAUSES" AT THE 21ST BIRTHDAY.
* THERE IS A JUMP, HOWEVER, IN MVA-RELATED DEATHS.
* THIS GIVES US CONFIDENCE THAT THE OBSERVED JUMP IN DEATHS IN ALL-CAUSE MORTALITY IS RELATED TO ALCOHOL.
		
* FINALLY, WE WILL USE OUR RD DUMMY IN A REGRESSION. 
* ASSUMING THAT OUR REASONING IS CORRECT, THE DUMMY WILL GIVE US AN UNBIASED ESTIMATE OF 
* THE CAUSAL EFFECT OF ALCOHOL CONSUMPTION ON THE MORTALITY OF YOUNG ADULTS.
												   
* TABLE 4.1 (P. 160)

* CREATE OUTCOME "OTHER EXTERNAL CAUSES OF DEATH"
gen ext_oth=external-homicide-suicide-mva
label variable ext_oth "Other external causes"

* LET'S PREDICT DIFFERENT CAUSES OF MORTALITY USING OUR RD DUMMY (COLUMN 1 OF TABLE 4.1).
* CENTERED AGE IS INCLUDED AS A CONTROL VARIABLE, TO ACCOUNT FOR CHANGES IN MORTALITY OVER TIME THAT ARE NOT RELATED TO THE 21ST BIRTHDAY.
reg all centered_age over21, robust 
reg mva centered_age over21, robust 
reg suicide centered_age over21, robust 
reg homicide centered_age over21, robust 
reg ext_oth centered_age over21, robust 
reg internal centered_age over21, robust 
reg alcohol centered_age over21, robust 

*OR, IN A LOOP 
foreach var of varlist all mva suicide homicide ext_oth internal alcohol {
	reg `var' centered_age over21, robust
	estimates store `var'
}

estimates table all mva suicide homicide ext_oth internal alcohol, stat(r2) b(%7.3g) se(%6.2g) p(%4.3f) varlabel

estimates drop all mva suicide homicide ext_oth internal alcohol
* WE CAN DROP THE STORED ESTIMATES FROM MEMORY AFTER USING THEM.


*WE CAN ADD OUR QUADRATIC TERM (COLUMN 2)
foreach var of varlist all mva suicide homicide ext_oth internal alcohol {
	reg `var' centered_age centered_age2 over21 over_bd2, robust
	estimates store `var'
}

estimates table all mva suicide homicide ext_oth internal alcohol, stat(r2) b(%7.3g) se(%6.2g) p(%4.3f) varlabel

estimates drop all mva suicide homicide ext_oth internal alcohol

* WE CAN ALSO RESTRICT THE BANDWIDTH OF THE ANALYSIS, FOR EXAMPLE TO ONE YEAR BEFORE AND AFTER THE CUTOFF.
* THIS MIGHT BE IMPORTANT IF WE SUSPECT THAT THE OBSERVATIONS FURTHER AWAY FROM TREATMENT MIGHT BE BIASING OUR RESULTS.

* COLUMN 3: AGES 20-22
foreach var of varlist all mva suicide homicide ext_oth internal alcohol {
	reg `var' centered_age over21 if agecell>=20 & agecell<=22, robust
	estimates store `var'
}

estimates table all mva suicide homicide ext_oth internal alcohol, stat(r2) b(%7.3g) se(%6.2g) p(%4.3f) varlabel

estimates drop all mva suicide homicide ext_oth internal alcohol

* COLUMN 4: AGES 20-22, QUADRATIC TERMS
foreach var of varlist all mva suicide homicide ext_oth internal alcohol {
	reg `var' centered_age centered_age2 over21 over_bd2 if agecell>=20 & agecell<=22, robust
	estimates store `var'
}

estimates table all mva suicide homicide ext_oth internal alcohol, stat(r2) b(%7.3g) se(%6.2g) p(%4.3f) varlabel

estimates drop all mva suicide homicide ext_oth internal alcohol

* THERE IS ALWAYS A TRADE-OFF BETWEEN STATISTICAL POWER AND POTENTIAL BIAS.
* MORE OBSERVATIONS MEANS MORE POWER, BUT ALSO MORE POTENTIAL BIAS.
* OBSERVATIONS FURTHER AWAY FROM THE CUTOFF MAY BE DIFFERENT FROM THOSE CLOSE TO IT.
* IF POSSIBLE, WE CAN TRY TO CONTROL FOR FACTORS THAT WE THINK ARE BIASING OUR RESULTS (PARAMETRIC RD).
* IN SUMMARY, THE SHARP RD SETUP RESTS ON THE ASSUMPTION THAT THE PERSONS BEFORE AND AFTER THE THRESHOLD CAN BE USED AS EACH OTHER'S COUNTERFACTUALS.
* IF THE RD IS WELL THOUGHT THROUGH, IT IS POSSIBLE TO GET CLOSER TO A CAUSAL EFFECT.


***FUZZY REGRESSION DISCONTINUITY (FRD) DESIGN***

* IN A FRD SETUP, THE PROBABILITY OF TREATMENT CHANGES SHARPLY AT THE CUTOFF POINT.
* MEHCANICALLY IT IS SIMILAR TO INSTRUMENTAL VARIABLES SETUP.
* THEN, WE USE THE THRESHOLD FOR TREATMENT AS AN INSTRUMENT, AND TWO-STAGE LEAST SQUARES REGRESSION.
* LOOK AT THE DO-FILE FROM LAB 3 FOR PRACTICAL EXAMPLE OF 2SLS.
* THE REQUIRED ASSUMPTIONS ARE THE SAME AS FOR IV REGRESSION.
* USE GRAPHING TO SEE IF THE PROBABILITY OF TREATMENT CHANGES AT THE CUTOFF POINT OF THE RUNNING VARIABLE, I.E. WHETHER THERE IS A FIRST STAGE.
* WE CAN ALSO ADD CONTROL VARIABLES (PARAMETRIC FRD).


log close
clear


*NOTE: SOME OF THE CODE COMES FROM https://github.com/vikjam/mostly-harmless-replication/
