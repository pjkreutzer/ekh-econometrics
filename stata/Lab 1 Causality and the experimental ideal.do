*****************************************************************
*                    EKHM66 Fall 2020                           *
*          Lab 1: Causality and the experimental ideal          *
*****************************************************************

/*TIP: EXECUTE COMMANDS FROM THE DO-FILE BY HIGHLIGHTING THE COMMANDS AND PRESSING CTRL+D.
PRESS THE PAGE UP KEY TO RETRIEVE THE MOST RECENTLY EXECUTED COMMAND TO THE COMMAND WINDOW.*/


***SETTING THE WORKING DIRECTORY***

cd "\\uwfpcluster01.uw.lu.se\el8285la$\Documents\PhD programme\Teaching\EKHM66 Econometrics II, Ht 2020\Labs\Lab 1 (Causality and the experimental ideal)"


***STARTING A LOG FILE***

log using "lab 1.log", replace


* IN THIS LAB, WE EXPLORE HOW SELECTION INTO TREATMENT LOOKS AND HOW RANDOMIZATION SOLVES THE SELECTION PROBLEM. 
* WE USE THE DATA USED IN THE COURSE BOOK (ANGRIST & PISCHKE 2014), WHERE THE FOCUS IS ON HEALTH OUTCOMES AND HEALTH INSURANCE.

***1. NHIS NON-EXPERIMENTAL DATA***

use NHIS2009_clean_subsample.dta, clear
* TO ILLUSTRATE A NON-EXPERIMENTAL SITUATION, WE LOOK CLOSER AT THE 2009 NATIONAL HEALTH INTERVIEW SURVEY (NHIS).

* THIS DATASET IS AN ANNUAL SURVEY, DESCRIBED AS "the principal source of information on the health of the civilian, noninstitutionalized, 
* household population of the United States." (ftp://ftp.cdc.gov/pub/health_statistics/nchs/dataset_documentation/nhis/2009/srvydesc.pdf)
* THE SAMPLING FOLLOWS A DESIGN AIMING AT PRODUCING A SAMPLE THAT IS REPRESENTATIVE OF THE POPULATION.
* A WEIGHING VARIABLE IS INCLUDED TO IMPROVE REPRESENTATIVENESS. 
* USING IT GIVES UNDERREPRESENTED GROUPS A HIGHER WEIGHT, AS RESPONSE RATES ARE NOT EQUALLY DISTRIBUTED ACROSS GROUPS.

* AUTHORS HAVE GENERATED SOME VARIABLES THAT ARE NOT LABELLED, SO WE'LL LABEL THEM OURSELVES.
label variable hlth "Health status"
label variable hi "Health Insurance coverage status"
label variable fml "Female"
label variable nwhite "Non-white"
label variable yedu "Years of education"
label variable empl "Employed"
label variable inc "Family income"
label variable famsize "Family size"

label define hi_lbl 0 "No health insurance" 1 "Some health insurance" 
label values hi hi_lbl  


***CHECKING FOR BALANCES (P. 16 IN THE BOOK)***

* BEFORE WE START DOING CAUSAL ANALYSIS, WE WANT TO MAKE SURE THAT WE KNOW OUR DATA WELL AND CHECK FOR BALANCES BETWEEN GROUPS.
* WE CAN ALREADY START THINKING ABOUT OUR TREATMENT, WHICH IN THIS CASE IS HEALTH INSURANCE. 
* DO WE EXPECT SELECTION INTO TREATMENT? WILL WE BE ABLE TO OBSERVE ALL POTENTIAL SELECTION ISSUES?

* THE FOLLOWING TABLE CORRESPONDS TO TABLE 1.1 ON PAGE 5 (NOTE: SAMPLE SIZES ARE NOT EXACTLY THE SAME AS IN THE BOOK.
* AUTHORS HAVE NOT FULLY DISCLOSED THEIR SAMPLING, BUT THE DESCRIPTIVE STATISTICS ARE STILL COMPARABLE).

* THE BOOK BREAKS DATA DOWN BY GENDER, SO WE DO THE SAME.

* FIRST, LET'S TAKE A BRIEF LOOK AT THE OUTCOME VARIABLE (SELF-RATED HEALTH SCORE) AND THE TREATMENT (HEALTHCARE).
hist hlth
tab hi
tab hi, sum(hlth)

* FIRST CELL IN TABLE 1.1 (MALES, MEAN HEALTH SCORE BY HEALTH INSURANCE STATUS)
* TYPE 'help weights' TO READ ABOUT THE USE OF WEIGHTS IN STATA.

* MALES
tab hi if fml==0, sum(hlth) //WITHOUT WEIGHTS, FOR COMPARISON
tab hi if fml==0 [aw=perweight], sum(hlth)  //WITH WEIGHTS (aw="analytic weight")

* FEMALES
tab hi if fml==1 [aw=perweight], sum(hlth)  //WITH WEIGHTS (aw="analytic weight")

* THE DIFFERENCES, AS A REGRESSION
reg hlth i.hi [aw=perweight] if fml==0
reg hlth i.hi [aw=perweight] if fml==1
* ADDING 'i.' IN FRONT OF A VARIABLE NAME TELLS STATA THAT IT SHOULD BE TREATED AS AN INDICATOR AND NOT NOT CONTINUOUS VARIABLE.

* THE TABLE IN ONE LINE OF CODE
* MALES (husbands)
tabstat hlth nwhite age yedu famsize empl inc [aw=perweight] if fml==0, by(hi) stat(mean) format(%4.2f)
* FEMALES (wives)
tabstat hlth nwhite age yedu famsize empl inc [aw=perweight] if fml==1, by(hi) stat(mean) format(%4.2f)

* THERE A A FEW WAYS TO EXPORT TABLES FROM STATA.
* DO NOT USE SCREENSHOTS, AS THEY CANNOT BE EDITED AND ARE NOT VERY NICE-LOOKING.
* INSTEAD, YOU CAN SELECT THE TABLE IN THE OUTPUT WINDOW, RIGHT-CLICK, AND USE "COPY TABLE".
* THEN YOU CAN PASTE THE TABLE INTO EXCEL FOR EDITING BEFORE INSERTING IT IN A TEXT EDITOR FILE.
* FOR EXPORTING EDITED TABLES DIRECTLY FROM STATA INTO WORD, LOOK UP FOR EXAMPLE THE COMMAND 'esttab'.

* WHAT DO WE OBSERVE IN THESE TABLES? 
* FIRSTLY, BOTH FOR MALES AND FEMALES, IT SEEMS THAT THE AVERAGE HEALTH STATUS IS HIGHER AMONG THE INSURED THAN THE UNINSURED. 
* JUDGING BY THE OTHER VARIABLES, IT SEEMS THAT INSURED PEOPLE IN THIS SURVEY ARE OLDER, MORE EDUCATED, HAVE SMALLER FAMILIES, 
* MORE LIKELY TO BE EMPLOYED, AND MORE LIKELY TO BE WHITE.

* ARE THE DIFFERENCES BETWEEN TREATED AND UNTREATED STATISTICALLY SIGNIFICANT?
* WE RUN AN INDEPENDENT GROUP T-TEST IN ORDER TO COMPARE MEANS OF AGE BETWEEN TREATED AND UNTREATED, WOMEN AND MEN SEPARATELY.
ttest age if fml==0, by(hi) 
ttest age if fml==1, by(hi) 
* BASED ON THE TESTS, WE REJECT THE NULL HYPOTHESIS THAT MEAN AGE IS THE SAME FOR INSURED AND UNINSURED MEN AND WOMEN.
* NOTE THAT WE ARE NOT USING WEIGHTS WITH ttest, SINCE THE WEIGHT FUNCTION IS NOT IMPLEMENTED IN THE COMMAND.

* IN ORDER TO SAVE TIME AND SPACE, WE CAN LOOP THIS TEST FOR EACH VARIABLE BY GENDER.
* YOU WILL NOT BE EXAMINED ON LOOPS DURING THIS COURSE, BUT YOU MIGHT WANT TO LEARN THEM FOR FUTURE USE.

foreach var in hlth nwhite age yedu famsize empl inc {
	display "`var'"
	ttest `var' if fml==0, by(hi) 
}

foreach var in hlth nwhite age yedu famsize empl inc {
	ttest `var' if fml==1, by(hi) 
}

* WE CAN NEST THE LOOPS FOR EVEN MORE EFFICIENCY.
foreach gender in 0 1 {
	foreach var in hlth nwhite age yedu famsize empl inc {
		ttest `var' if fml==`gender', by(hi) 
		display "This is the t-test of `var' where female = `gender'"
	}
}

* USING LINEAR REGRESSION WILL THEORETICALLY GIVE US THE SAME TEST AS THE ttest COMMAND.
* THE COEFFICIENT FOR HEALTH INSURANCE REPRESENTS THE DIFFERENCE IN MEANS BETWEEN INSURED/UNINSURED IN THE DEPENDENT VARIABLE.
reg age i.hi if fml==0 
reg age i.hi if fml==0 [aw=perweight], robust //THE robust OPTION PRODUCES HETEROSKEDASTICITY-ROBUST STANDARD ERRORS
reg yedu i.hi if fml==0 [aw=perweight], robust
reg nwhite i.hi if fml==0 [aw=perweight], robust

*OR, ALL IN A LOOP
foreach gender in 0 1 {
	foreach var in hlth nwhite age yedu famsize empl inc {
		reg `var' i.hi if fml==`gender' [aw=perweight], robust
		display "`var' where female = `gender'"
	}
}

* IT SEEMS THAT, APART FROM THE VARIABLE 'nwhite', THERE ARE STATISTICALLY SIGNIFICANT DIFFERENCES IN BOTH DEMOGRAPHIC VARIABLES
* AND HEALTH STATUS BETWEEN THE INSURED AND THE UNINSURED IN THE NHIS SAMPLE. 
* DOES THE RESULT SUGGEST THAT HEALTH INSURANCE IMPROVES HEALTH STATUS? NO.
* WHAT IF WE CONTROL FOR THE OBSERVABLE DIFFERENCES IN OUR REGRESSIONS? DO WE THEN ESTIMATE THE CAUSAL EFFECT OF INSURANCE ON HEALTH?

* DEPENDS!
* IS IT POSSIBLE THAT THERE ARE HEALTH-RELATED FACTORS THAT MATTER FOR HEALTH OUTCOMES BUT WHICH OUR DATA ARE NOT CAPTURING?
* IN OTHER WORDS, IS IT POSSIBLE THAT EVEN IF WE CONTROL FOR OBSERVABLES, THERE IS OMITTED VARIABLE BIAS?

* A CONTROL STRATEGY HINGES ALSO ON THE RELIABILITY AND VALIDITY OF THE CONTROL VARIABLES.
* IN SUM, IF WE DRAW CONCLUSIONS ABOUT THE OUTCOMES OF HEALTH INSURANCE ON THESE DATA, WE HAVE TO BE VERY CAREFUL 
* AROUND INTERPRETING CAUSALITY AND DIRECTION OF THE EFFECT.
* AN EXPERIMENTAL SITUATION WITH RANDOM ASSIGNMENT TO TREATMENT CAN SOLVE THE SELECTION PROBLEM.



***2. RAND EXPERIMENTAL DATA***

* THE RAND HEALTH EXPERIMENT SET OUT TO ANSWER QUESTIONS ON HEALTH AND CONSUMPTION OF HEALTH CARE, SUCH AS:
	* WHAT HAPPENS TO HEALTH CARE USE AS INSURANCE PRICE GOES UP (WHAT IS THE PRICE ELASTICITY OF HEALTH CARE DEMAND)?
	* HOW ARE HEALTH OUTCOMES AFFECTED BY ACCESS TO A BETTER INSURANCE PLAN?

* THESE QUESTIONS ARE VERY HARD TO ANSWER USING THE NHIS DATA, AS THE POTENTIAL HEALTH OUTCOMES OF THE INSURED AND UNINSURED ARE LIKELY NOT SIMILAR.
* IN THE RAND EXPERIMENT, HOWEVER, THE DESIGN WAS TO RANDOMIZE WHICH HEALTH INSURANCE PLAN FAMILIES USED. 
* THIS WOULD, THEORETICALLY, MAKE THE INSURED (TREATED) AND UNINSURED (UNTREATED) HAVE SAME POTENTIAL OUTCOMES.
* ANY DIFFERENCES BETWEEN THE TREATED AND THE UNTREATED CAN THEN BE CAUSALLY ATTRIBUTED TO THE TREATMENT (INSURANCE)!

use rand_subsample.dta, clear

* NOTE: THIS DATASET DOES NOT INCLUDE ANY SAMPLE WEIGHTING VARIABLE AS THE NHIS DATA DID.
* ALWAYS CHECK DATA DOCUMENTATION WHEN WORKING WITH A NEW DATASET TO FIND OUT IF WEIGHTING IS NECESSARY OR NOT.

* THIS IS THE DISTRIBUTION OF INDIVIDUALS ACROSS INSURANCE PLANS.
* WE LOOK AT "ANY INSURANCE" AS TREATMENT, RATHER THAN THE DIFFERENT PLAN TYPES.
tab any_ins plantype

***CHECKING FOR BALANCES***

* LET'S CHECK FOR BALANCES BETWEEN TREATED AND UNTREATED AND COMPARE TO THE NON-EXPERIMENTAL SETUP FROM ABOVE.
* NOTE THAT THE HEALTH MEASURES IN THIS TABLE ARE PRE-TREATMENT (VARIABLES DENOTED "AT ENROLLMENT").
tabstat female blackhisp age educper income1cpi hosp ghindx cholest systol mhi, by(any_ins) stat(mean) format(%4.3f)
*THESE DATA CAN BE COMPARED TO THE DATA IN TABLE 1.3, COLUMNS (1) AND (5), P. 20 IN THE BOOK.

* ANOTHER INTUITIVE WAY OF COMPARING MEANS IS BY USING A BOXPLOT.
* A BOXPLOT VISUALISES THE VALUES OF A GIVEN NUMERICAL VARIABLE BASED ON QUARTILES.
graph box cholest, by(any_ins)

* BASED ON VISUAL ASSESSMENT, THE DIFFERENCES BETWEEN THE GROUPS ARE SMALL, WHICH IS REASSURING. 
* BUT ARE THE DIFFERENCES STATISTICALLY SIGNIFICANT?
* WE RUN T-TESTS AND REGRESSIONS TO FIND OUT, SETTING THE DECISION RULE AT ALPHA=0.05.
ttest cholest, by(any_ins) 
reg cholest any_ins
ttest female, by(any_ins) 
reg female any_ins

* WE REJECT THERE BEING ARE ANY STATISTICALLY SIGNIFICANT DIFFERENCES BETWEEN TREATED/UNTREATED IN THESE TWO VARIABLES. 

* BUT...

* THE STANDARD ERRORS FROM OUR T-TESTS ARE NOT THE SAME AS IN THE BOOK.
* THIS IS BECAUSE THE AUTHORS HAVE CLUSTERED THE STANDARD ERRORS AT FAMILY LEVEL.
* SEE P. 207-208 IN THE BOOK FOR CLUSTERED STANDARD ERRORS.
* CLUSTERING ERRORS CANNOT BE DONE IN A T-TEST, SO WE HAVE TO USE REGRESSION. 
* WE CLUSTER THE STANDARD ERRORS AT FAMILY LEVEL LEVEL USING THE cl() OPTION AND A FAMILY IDENTIFIER VARIABLE.

reg female any_ins, cl(famid)
reg cholest any_ins, cl(famid)

* WHEN WE CLUSTER THE STANDARD ERRORS, THERE IS A STATISTICALLY SIGNIFICANT DIFFERENCE IN SHARE OF WOMEN ACROSS THE TREATED AND UNTREATED GROUPS.
* HOWEVER, THIS DIFFERENCE IS SMALL IN MAGNITUDE.

* YOU CAN RUN THE TESTS FOR EACH VARIABLE ONE BY ONE.
* YOU CAN ALSO TEST ALL OF THE VARIABLES AT ONCE BY USING A LOOP, PRODUCING THE DATA IN TABLE 1.3 WITH ONE COMMAND.

foreach var of varlist female blackhisp age educper income1cpi hosp ghindx cholest systol mhi {
	reg `var' any_ins, cl(famid)
	estimates store `var'
}
estimates table female blackhisp age educper income1cpi hosp ghindx cholest systol mhi, stat(r2) b(%7.3g) se(%6.2g) p(%4.2f) 
* THIS TABLE CORRESPONDS TO COLUMN (5) IN TABLE 1.3 P. 20, WHERE WE LOOK AT THE DIFFERENCE IN MEANS BETWEEN TREATED/UNTREATED.
* THE STATISTICS SHOWN ARE THE ESTIMATED DIFFERENCE IN MEANS, ITS STANDARD ERROR, P-VALUE, AND R-SQUARED.
* THE SAME INFORMATION IS ALSO SHOWN FOR THE CONSTANT, WHICH CORRESPONDS TO MEANS SHOWN IN COLUMN (1), FOR THE UNTREATED GROUP. 

* THESE PRE-TREATMENT VARIABLES, BOTH SOCIO-ECONOMIC AND HEALTH-RELATED, SUGGEST THAT IN THIS SAMPLE, 
* THERE ARE NO LARGE AND STATISTICALLY SIGNIFICANT DIFFERENCES BETWEEN THE TREATED AND THE UNTREATED GROUPS.
* WE NOTE A SMALL AND STATISTICALLY SIGNIFICANT DIFFERENCE IN THE SHARE OF WOMEN. THIS COULD BE DUE TO RANDOM VARIATION. 
* IF THE ASSIGNMENT TO TREATMENT IS TRULY RANDOM, WHAT WE SEE IN TABLE 1.3 CORRESPONDS TO OUR EXPECTATIONS. 
* IT ALSO MEANS IS THAT ANY GROUP DIFFERENCES IN POST-TREATMENT OUTCOMES CAN BE CAUSALLY LINKED TO THE TREATMENT!


***ESTIMATING THE CAUSAL EFFECT OF HEALTH INSURANCE ON HEALTH***

* LET'S LOOK AT HEALTH OUTCOMES BETWEEN TREATED/UNTREATED POST-TREATMENT (VARIABLES DENOTED "AT EXIT").

* THIS LIST CORRESPONDS TO COLUMN (5) IN TABLE 1.4, PANEL B.
foreach var of varlist ghindxx cholestx systolx mhix {
	reg `var' any_ins, cl(famid)
	estimates store `var'
}
estimates table ghindxx cholestx systolx mhix, stat(r2) b(%7.3g) se(%6.3g) p(%4.3f)
* THIS LIST CORRESPONDS TO COLUMN (5) IN TABLE 1.4, PANEL B.
* WHAT DOES THE TABLE SUGGEST ABOUT THE HEALTH EFFECTS OF HEALTH INSURANCE?

* BASED ON THE RAND EXPERIMENT, THERE ARE NO HEALTH BENEFITS FROM HAVING INSURANCE.
* HOW DOES THIS CONCLUSION DIFFER FROM WHAT WE WOULD HAVE INFERRED FROM THE NON-EXPERIMENTAL NHIS DATA?
* THE NHIS DATA SUGGESTED THAT INSURANCE HOLDERS WERE, ON AVERAGE, HEALTHIER THAN NON INSURANCE-HOLDERS.
* WHAT DOES THE DIFFERENCE IN THE CONCLUSIONS FROM ANALYSIS ON NON-EXPERIMENTAL AND EXPERIMENTAL DATA TELL US?
* WE ALWAYS NEED TO REMEBER THAT CORRELATION IS NOT CAUSATION! 
* WE NEED THE CETERIS PARIBUS CONDITION TO BE FULFILLED IN ORDER TO MAKE CAUSAL INTERPRETATIONS.

* NOTE: WE HAVE ONLY RUN BI-VARIATE REGRESSIONS IN THIS LAB! 
* THIS IS BECAUSE WE WANTED TO FOCUS ON DISCUSSING THE EXPERIMENTAL IDEAL.
* NORMALLY ECONOMETRIC MODELS INVOLVE MULTIPLE VARIABLES, AS EXPERIMENTAL DATA TENDS TO BE SCARCE.
* NEXT TIME, WE EXAMINE OLS REGRESSION AND MULTIVARIATE MODELS.

log close
clear
